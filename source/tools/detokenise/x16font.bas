1     CR=1
2     CC=1
3     FR=1
4     FC=1
5     K1=$60
6     RZ=CR
7     CZ=CC
8     RY=FR
9     CY=FC
10    DIMRO(60):REM ROW OFFSET FOR THE VIDEO POKE TRICKS
11    FORI=$B0TO$EBSTEP1
12    RO(I-$AF)=I:REM USE INDEX 1 TO 60
13    NEXTI
14    COLOR1,6:REM WHITE ON BLUE
15    SCREEN3:REM 40 COL, 30 ROW
16    COLOR0,6
17    LOCATE1,39
18    PRINT"V*";
19    COLOR4,6
20    LOCATE1,2
21    PRINT"DISPLAY-CODES";
22    LOCATE1,24
23    PRINT"CURRENT-FONT";
24    LOCATE21,1
25    PRINT"PRINT-CODES (HEX $)";
26    COLOR1,6
27    GOSUB249
28    GOSUB117
29    GOSUB136
30    GOSUB58
31    REM GET KEYBOARD INPUT
32    GETA$
33    A=ASC(A$):REM CONVERT STRING TO NUMERIC
34    REM CHARACTER MAP DISPLAY MOVEMENT
35    IFA=87THENRZ=RZ-1:GOSUB58:REM 'W'
36    IFA=83THENRZ=RZ+1:GOSUB58:REM 'S'
37    IFA=65THENCZ=CZ-1:GOSUB58:REM 'A'
38    IFA=68THENCZ=CZ+1:GOSUB58:REM 'D'
39    REM FONT DISPLAY MOVEMENT
40    IFA=145THENRY=RY-1:GOSUB58:REM UP ARROW
41    IFA=17THENRY=RY+1:GOSUB58:REM DOWN ARROW
42    IFA=157THENCY=CY-1:GOSUB58:REM LEFT ARROW
43    IFA=29THENCY=CY+1:GOSUB58:REM RIGHT ARROW		
44    IFA=32THENGOSUB147:GOSUB58:REM SPACEBAR
45    IFA=73THENGOSUB301:GOSUB58:REM I FOR INVERT
46    IFA=67THENGOSUB264:GOSUB58:REM C FOR CLEAR ALL
47    IFA=69THENGOSUB274:GOSUB58:REM S FOR SET ALL
48    IFA=75THENGOSUB284:GOSUB58:REM K FOR COPY
49    IFA=86THENGOSUB197:REM SAVE V
50    IFA=76THENGOSUB224:GOSUB58:REM LOAD L
51    IFA=88THENGOSUB158:REM X EXPORT COMMAND			
52    ZZ=0:REM DON'T UPDATE THE DISPLAY-FONTS
53    IFA=93THENK1=K1+8:ZZ=1:IF(K1>192)THENK1=0:REM KEY ] INCREMENT
54    IFA=91THENK1=K1-8:ZZ=1:IF(K1<0)THENK1=192:REM KEY [ DECREMENT
55    IF(ZZ=1)THENGOSUB117
56    IFA=27THENGOTO355:REM ESCAPE
57    GOTO31
58    X=CC+1
59    Y=CR+1
60    D=$61
61    GOSUB348
62    PRINT""
63    COLOR13,6
64    REM UPDATE CHARACTER DISPLAY MARKERS
65    LOCATECR+1,18
66    PRINT" ";
67    IF(RZ>16)THENRZ=1
68    IF(RZ<1)THENRZ=16
69    CR=RZ
70    LOCATECR+1,18
71    PRINTCHR$($5F);
72    LOCATE18,CC+1
73    PRINT" ";
74    IF(CZ>16)THENCZ=1
75    IF(CZ<1)THENCZ=16
76    CC=CZ
77    LOCATE18,CC+1
78    PRINT"^";
79    REM UPDATE FONT DISPLAY MARKERS
80    LOCATEFR+1,38
81    PRINT" ";
82    IF(RY>8)THENRY=1
83    IF(RY<1)THENRY=8
84    FR=RY
85    LOCATEFR+1,38
86    PRINTCHR$($5F);
87    LOCATE10,FC+29
88    PRINT" ";
89    IF(CY>8)THENCY=1
90    IF(CY<1)THENCY=8
91    FC=CY
92    LOCATE10,FC+29
93    PRINT"^";
94    D=$F000+((((CR-1)*16)+(CC-1))*8)
95    COLOR3,6
96    FORI=1TO8
97    LOCATE1+I,24
98    PRINT"$";HEX$(D);
99    V=VPEEK(1,D)
100   LOCATE1+I,30
101   PRINTBIN$(V);
102   D=D+1
103   NEXTI
104   LOCATE1,17
105   PRINT"    ";
106   LOCATE1,17
107   PRINT(((CR-1)*16)+(CC-1))
108   X=CC+1
109   Y=CR+1
110   D=$E7
111   GOSUB348
112   X=FC+29
113   Y=FR+1
114   REM (USE SAME COLOR)
115   GOSUB348
116   RETURN
117   X=1
118   Y=22
119   I=K1
120   I2=0
121   COLOR1,6
122   ZZ=0
123   IF((I>=32)AND(I<=127))THENZZ=1:REM PRINTABLE
124   IF((I>=160)AND(I<=255))THENZZ=1:REM PRINTABLE	
125   LOCATEY,X:PRINT"   ";
126   LOCATEY,X:PRINTHEX$(I);
127   LOCATEY,X+3
128   IF(ZZ=0)THENPRINT".";:REM NON-PRINTABLE
129   IF(ZZ<>0)THENPRINTCHR$(I);
130   X=X+5:I2=I2+1
131   IF(X>40)THENY=Y+1:X=1
132   I=I+1
133   IF(I2>63)THENGOTO135
134   GOTO122
135   RETURN
136   X=2
137   Y=2
138   D=0
139   IF(D>255)THENGOTO145
140   GOSUB343
141   X=X+1
142   IF(X>17)THENY=Y+1:X=2
143   D=D+1
144   GOTO139
145   REM DISPLAY END
146   RETURN
147   D=$F000+((((CR-1)*16)+(CC-1))*8)
148   D=D+(FR-1):REM INCREMENT THE FONT ROW	
149   V=VPEEK(1,D)
150   T=256
151   FORI=FCTO1STEP-1
152   T=INT(T/2)
153   NEXTI
154   V=(V+T)-2*(VANDT)
155   VPOKE1,D,V
156   GOSUB353
157   RETURN
158   COLOR7,6
159   LOCATE2,19
160   PRINT"WAIT";
161   LOCATE3,19
162   PRINT"EXP";
163   U1=$F000
164   U2=U1+127
165   IN=1
166   X=28000+(CR*2000):REM BASIC LINE NUMBER	
167   I2=0
168   I3=1
169   IF(IN=CR)THENGOTO172
170   I2=I2+128:REM ACCOUNT FOR NOT OUTPUTING THIS
171   GOTO188
172   F$="@:FONT"+STR$(IN)+".BAS,S,W":REM SEQUENTIAL WRITE
173   OPEN1,8,2,F$
174   LOCATE19,1
175   PRINT"     "
176   LOCATE19,1
177   PRINTST
178   FORI=U1TOU2
179   IF(I3=1)THENPRINT#1,X;" REM FONT ";INT(I2/8):X=X+10
180   V=VPEEK(1,I)
181   PRINT#1,X;" VPOKE 1,$";HEX$(I);",%",BIN$(V)
182   X=X+10
183   I3=I3+1
184   IF(I3>8)THENI3=1
185   I2=I2+1
186   NEXTI
187   CLOSE1
188   IF(IN<16)THENIN=IN+1:U1=U1+128:U2=U2+128:GOTO169
189   COLOR1,6
190   LOCATE2,19
191   PRINT"    ";
192   LOCATE3,19
193   PRINT"    ";
194   LOCATE4,19
195   PRINT"    ";
196   RETURN
197   COLOR7,6
198   LOCATE2,19
199   PRINT"WAIT";
200   LOCATE3,19
201   PRINT"SAVE";
202   F$="@:X16FONT"+STR$(CR)+".DAT,S,W":REM SEQUENTIAL WRITE
203   OPEN1,8,2,F$
204   LOCATE19,1
205   PRINT"     "
206   LOCATE19,1
207   PRINTST
208   IF(ST=0)THENLOCATECR+1,1:PRINT" ";
209   U1=$F000+(CR-1)*128
210   U2=U1+127:REM 16 BYTES PER ROW
211   FORI=U1TOU2:REM $F000 TO $F7FF	  
212   V=VPEEK(1,I)
213   PRINT#1,V
214   NEXTI
215   CLOSE1
216   COLOR1,6
217   LOCATE2,19
218   PRINT"    ";
219   LOCATE3,19
220   PRINT"    ";
221   LOCATE4,19
222   PRINT"    ";
223   RETURN
224   COLOR7,6
225   LOCATE2,19
226   PRINT"WAIT";
227   LOCATE3,19
228   PRINT"LOAD";
229   F$="X16FONT"+STR$(CR)+".DAT,S,R":REM SEQUENTIAL READ
230   OPEN1,8,2,F$
231   LOCATE19,1
232   PRINT"     "
233   LOCATE19,1
234   PRINTST
235   IF(ST=0)THENLOCATECR+1,1:PRINT" ";
236   U1=$F000+(CR-1)*128
237   U2=U1+127:REM 8 BYTES PER FONT
238   FORI=U1TOU2:REM $F000 TO $F7FF	  		
239   INPUT#1,V
240   VPOKE1,I,V
241   NEXTI
242   CLOSE1
243   COLOR1,6
244   LOCATE2,19
245   PRINT"    ";
246   LOCATE3,19
247   PRINT"    ";
248   RETURN
249   COLOR12,6:REM    1234567890123456789012
250   LOCATE11,19:PRINT"ARROW KEYS   MOVE FONT";
251   LOCATE12,19:PRINT"W-A-S-D   MOVE DISPLAY";
252   LOCATE13,19:PRINT"SPACE  TOGGLE FONT BIT";
253   LOCATE14,19:PRINT"V  SAVE   X16FONTN.DAT";
254   LOCATE15,19:PRINT"L  LOAD   X16FONTN.DAT";
255   LOCATE16,19:PRINT"X  EXPORT    FONTN.BAS";
256   LOCATE17,19:PRINT"I  INVERT ROW-COL-BOTH";
257   LOCATE18,19:PRINT"C  CLEAR ALL/E=SET ALL";
258   LOCATE19,19:PRINT"[ ]  DEC / INC DISPLAY";
259   LOCATE20,19:PRINT"K  COPY     / ESC EXIT";
260   REM                 1234567890123456789012345678901234567890
261   LOCATE30,1:PRINT"USE SHIFT+ALT TO RESTORE ORIGINAL FONT";
262   COLOR1,6
263   RETURN
264   D=$F000+((((CR-1)*16)+(CC-1))*8)
265   CA=FR
266   FORFR=1TO8
267   D1=D+(FR-1)
268   V=$00
269   VPOKE1,D1,V
270   NEXTFR
271   FR=CA
272   GOSUB353
273   RETURN
274   D=$F000+((((CR-1)*16)+(CC-1))*8)
275   CA=FR
276   FORFR=1TO8
277   D1=D+(FR-1)
278   V=$FF
279   VPOKE1,D1,V
280   NEXTFR
281   FR=CA
282   GOSUB353
283   RETURN
284   LOCATE21,18
285   COLOR7,6
286   PRINT"WHICH (0-255)";
287   INPUTA
288   IF(A=(((CR-1)*16)+(CC-1)*8))THENGOTO296:REM TARGET/SOURCE IS SAME
289   D=$F000+((((CR-1)*16)+(CC-1))*8):REM TARGET
290   D1=$F000+(A*8):REM SOURCE
291   FORI=0TO7
292   V=VPEEK(1,D1+I)
293   VPOKE1,D+I,V
294   NEXTI
295   GOSUB353
296   LOCATE21,18
297   COLOR1,6
298   PRINT"                      ";
299   A=0
300   RETURN
301   PRINT""
302   LOCATE21,18
303   COLOR7,6
304   PRINT"C=COL,R-ROW,B-BOTH/ESC";
305   A=0
306   GETA$
307   A=ASC(A$)
308   IFA=0THENGOTO306
309   IFA=67THENGOTO323:REM OPTION C
310   IFA=82THENGOTO330:REM OPTION R
311   IFA=27THENGOTO337:REM ESCAPE
312   CA=FC
313   CB=FR
314   FORFC=1TO8
315   FORFR=1TO8
316   GOSUB147
317   NEXTFR
318   NEXTFC
319   FC=CA
320   FR=CB
321   GOSUB353
322   GOTO337
323   CA=FR
324   FORFR=1TO8
325   GOSUB147
326   NEXTFR
327   FR=CA
328   GOSUB353
329   GOTO337
330   CA=FC
331   FORFC=1TO8
332   GOSUB147
333   NEXTFC
334   FC=CA
335   GOSUB353
336   GOTO337
337   LOCATE21,18
338   COLOR1,6
339   REM    1234567890123456789012
340   PRINT"                      ";
341   A=0
342   RETURN
343   POKE$9F20,(X-1)*2
344   POKE$9F21,RO(Y)
345   POKE$9F22,$21
346   POKE$9F23,D
347   RETURN
348   POKE$9F20,((X-1)*2)+1
349   POKE$9F21,RO(Y)
350   POKE$9F22,$21
351   POKE$9F23,D
352   RETURN
353   LOCATECR+1:PRINT"*";
354   RETURN
355   LOCATE29,1
356   END
