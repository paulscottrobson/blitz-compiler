; ************************************************************************************************
; ************************************************************************************************
;
;		Name:		hex.asm
;		Purpose:	Hexadecimal conversion
;		Created:	11th April 2023
;		Reviewed: 	No
;		Author:		Paul Robson (paul@robsons.org.uk)
;
; ************************************************************************************************
; ************************************************************************************************

		.section code
		
; ************************************************************************************************
;
;								BIN$ (integer to string)
;
; ************************************************************************************************

Unary16Hex: ;; [hex$]
		.entercmd
		jsr 	GetInteger16Bit
		lda 	#4 							; allocate / set 4 bytes.
		jsr 	StringAllocTemp
		lda 	zTemp0+1
		beq 	_UHNoHigh
		jsr 	_UHWriteHex
_UHNoHigh:
		lda 	zTemp0
		jsr 	_UHWriteHex
		.exitcmd

_UHWriteHex:		
		pha
		lsr 	a
		lsr 	a
		lsr 	a
		lsr 	a
		jsr 	_UHWriteNibl
		pla
_UHWriteNibl:				
		and 	#15
		cmp 	#10
		bcc 	_UHDigit
		adc 	#6
_UHDigit:
		adc 	#48
		jsr 	StringWriteChar
		rts

		.send code

; ************************************************************************************************
;
;									Changes and Updates
;
; ************************************************************************************************
;
;		Date			Notes
;		==== 			=====
;
; ************************************************************************************************
