10    GOTO 1150
15    REM ** LINES 100-399 : PATTERN FORMULA **
100   REM PATTERN 1
105   IF INT(P1/2)*2-INT(P1)=INT(P2/2)*2-INT(P2) THEN PSET S-SA,TT,0:RETURN
110   REM PATTERN 2
115   REM IF P1-INT(P1) < P2-INT(P2)-0.3 THEN PSET S-SA,TT,0:RETURN
120   REM PATTERN 3
125   REM IF P1-INT(P1) < 0.3 OR P2-INT(P2) < 0.3 THEN PSET S-SA,TT,0:RETURN
210   REM PATTERN 4
215   REM CA=SQR(P1*P1+P2*P2)
220   REM IF CA-INT(CA) < 0.4 THEN PSET S-SA,TT,0:RETURN
300   RETURN
394   :
400   FOR S=SO TO SP:P1=S*CC+CD:P2=S*CE+CF:GOSUB 100:NEXT:RETURN
404   :
405   REM ** EDGE OF SPHERE **
410   CA=D+C*T:DI=CA*CA-A*(T*T*B+T*E+F):IF DI<0 THEN F0=-1:RETURN
420   CB=SQR(DI)*SGN(A):S1=(CA-CB)/A:S2=(CA+CB)/A:F1=S1*MI+T*MJ+MK>0
430   F2=S2*MI+T*MJ+MK>0:S1=INT(S1+.5):S2=INT(S2+.5):IF F1 AND F2 THEN 520
440   IF F1 THEN 490
450   IF NOT F2 THEN F0=-1:RETURN
460   IF S2>SB THEN F0=-1:RETURN
470   IF S2<SA THEN S1=SA:S2=SB:F1=0:F2=0:F0=0:RETURN
480   S1=S2:S2=SB:F1=-1:F2=0:F0=0:RETURN
490   IF S1<SA THEN F0=-1:RETURN
500   IF S1>SB THEN S1=SA:S2=SB:F2=0:F1=0:F0=0:RETURN
510   S2=S1:S1=SA:F2=-1:F1=0:F0=0:RETURN
520   IF S1>SB OR S2<SA THEN F0=-1:RETURN
530   IF S1<SA THEN S1=SA:F1=0
540   IF S2>SB THEN S2=SB:F2=0
550   F0=0:RETURN
554   :
555   REM ** EDGE OF SHADOW **
560   CA=Q+T*P:DI=CA*CA-N*(T*T*O+T*R+U):IF DI<0 THEN F5=-1:RETURN
570   CB=SQR(DI)*SGN(N):S3=(CA-CB)/N:S4=(CA+CB)/N:F1=S3*V+T*W+X<0:F2=S4*V+T*W+X<0
580   S3=INT(S3+.5):S4=INT(S4+.5):IF F1 AND F2 THEN 680
590   IF F1 THEN 650
600   IF NOT F2 THEN F5=-1:RETURN
620   IF S4>SB THEN F5=-1:RETURN
630   IF S4<SA THEN S3=SA:S4=SB:F5=0:RETURN
640   REM S3=S4:S3=SB:F5=0:RETURN
641   S3=S4:S4=SB:F5=0:RETURN
650   IF S3<SA THEN F5=-1:RETURN
660   IF S3>SB THEN S3=SA:S4=SB:F5=0:RETURN
670   S4=S3:S3=SA:F5=0:RETURN
680   IF S3>SB OR S4<SA THEN F5=-1:RETURN
690   IF S3<SA THEN S3=SA
700   IF S4>SB THEN S4=SB
710   F5=0:RETURN
715   :
720   IF S3>SA THEN SO=SA:SP=S3-1:GOSUB 400:RETURN
730   RETURN
740   IF S1>SA THEN SO=SA:SP=S1-1:GOSUB 400:RETURN
750   RETURN
760   IF S4<SB THEN SO=S4+1:SP=SB:GOSUB 400:RETURN
770   RETURN
780   IF S2<SB THEN SO=S2+1:SP=SB:GOSUB 400:RETURN
790   RETURN
794   :
795   REM ** MAIN LOOP **
800   T=TA-1:GOSUB 410:F4=NOT F0:F3=F0:SCREEN $80
810   FOR T=TA TO TB:TT=TB-T:SQ=S1:SR=S2:GOSUB410:IF F0 THEN 980
820   F4=-1:IF F3 THEN LINE S1-SA,TT,S2-SA,TT,0:F3=0:GOTO990
830   IF S2-S1<2 THEN 930
831   :
835   REM ** THE INNER OF THE SPHERE **
840   FOR S=S1+1 TO S2-1:Z1=S*A1+T*B1+C1:Z2=S*A2+T*B2+C2:Z3=T*B3+C3
850   CA=Z1*Z1+Z2*Z2+Z3*Z3:CB=M1*Z1+M2*Z2+M3*Z3:J=(CB-SQR(CB*CB-CA*Y))/CA
860   P1=J*Z1+Q1:P2=J*Z2+Q2:P3=J*Z3+Q3:R1=P1-K1:R2=P2-K2:R3=P3-K3
870   CA=2*(Z1*R1+Z2*R2+Z3*R3)/RR:Z1=Z1-CA*R1:Z2=Z2-CA*R2:Z3=Z3-CA*R3
880   IF Z3>=0 THEN 920
890   J=-P3/Z3:P1=P1+J*Z1:P2=P2+J*Z2:IF P1*N1+P2*N2<Z THEN GOSUB 100:GOTO 920
900   IF P1*P1*G+P2*P2*H+P1*P2*I+P1*K+P2*L+M<0 THEN GOSUB 100:GOTO 920
910   PSET S-SA,TT,0
920   NEXT
921   :
925   REM ** EDGE OF SPHERE **
930   IF SQ<>S1 THEN LINE SQ+SGN(S1-SQ)-SA,TT,S1-SA,TT,0:GOTO 950
940   IF F1 THEN PSET S1-SA,TT,0
950   IF SR<>S2 THEN LINE SR+SGN(S2-SR)-SA,TT,S2-SA,TT,0:GOTO 990
960   IF F2 THEN PSET S2-SA,TT,0:GOTO 990
970   GOTO 990
975   :
980   IF F4 THEN LINE SQ-SA,TT,SR-SA,TT,0:F4=0
984   :
985   REM ** THE PLANE **
990   REM IF T>=-C3/B3 THEN NEXT:GOTO 1130
991   IF T>=-C3/B3 THEN NEXT:GOTO 1126
995   REM ** THE PLANE **
1000  CA=T*B3+C3:CC=-Q3*A1/CA:CD=(Q1-Q3*(T*B1+C1)/CA)
1010  CE=-Q3*A2/CA:CF=(Q2-Q3*(T*B2+C2)/CA)
1020  GOSUB 560:IF F0 AND F5 THEN SO=SA:SP=SB:GOSUB 400:GOTO 1120
1030  IF F0 THEN GOSUB 720:GOSUB 760:GOTO 1110
1040  IF F5 THEN GOSUB 740:GOSUB 780:GOTO 1120
1050  IF S4<S1-1 THEN GOSUB 720:SO=S4+1:SP=S1-1:GOSUB 400:GOSUB 780:GOTO 1110
1060  IF S3>S2+1 THEN GOSUB 740:SO=S2+1:SP=S3-1:GOSUB 400:GOSUB 760:GOTO 1110
1070  IF S1>S3 THEN GOSUB 720:LINE S3-SA,TT,S1-SA,TT,0:GOTO 1090
1080  GOSUB 740
1090  IF S4>S2 THEN LINE S2-SA,TT,S4-SA,TT,0:GOSUB 760:GOTO 1120
1100  GOSUB 780:GOTO 1120
1110  LINE S3-SA,TT,S4-SA,TT,0
1120  NEXT
1125  :
1126  POKE 198,0
1127  GET G$:IF G$="" THEN 1127
1130  REM ** DONE **
1140  END
1141  :
1142  :
1145  REM ** PROGRAM START
1146  REM    INPUT, CALCULATION OF PARAMETERS AND VECTORS **
1150  S=1:CA=1:CB=1:T=1:Z1=1:Z2=1:Z3=1:P1=1:P2=1:P3=1:J=1:R1=1:R2=1:R3=1:A1=1
1160  A2=1:B1=1:B2=1:B3=1:C1=1:C2=1:C3=1:K1=1:K2=1:K3=1:M1=1:M2=1:M3=1:Q1=1:Q2=1
1170  Q3=1:TT=1:N1=1:N2=1:Z=1:Y=1:G=1:H=1:I=1:L=1:M=1:RR=1
1180  CC=1:CD=1:CE=1:CF=1:F1=1:F2=1:SA=1:SB=1
1190  DI=1:F0=1:F5=1:SO=1:SP=1:S1=1:S2=1:S3=1:S4=1:SQ=1:SR=1:F3=1:F4=1:V=1:W=1
1200  X=1:MI=1:MJ=1:MK=1:A=1:B=1:C=1:D=1:E=1:F=1:Q=1:P=1:N=1:O=1:R=1:U=1
1205  READ K1,K2,K3,RR:RR=RR*RR
1206  READ L1,L2,L3,Q1,Q2,Q3,C1,C2,C3,CA,CB,CC,CD:GOTO 1280
1210  PRINT "THE SPHERE:":INPUT "COORDINATES OF CENTER ";K1,K2,K3
1220  INPUT "RADIUS ";RR:RR=RR*RR
1230  PRINT:INPUT "COORDINATES OF LAMP ";L1,L2,L3
1240  INPUT "COORDINATES OF CAMERA ";Q1,Q2,Q3
1250  INPUT "YOUR VIEWING VECTOR ";C1,C2,C3
1260  PRINT:INPUT "APERTURE ANGLE HOR/VER ";CA,CB
1270  INPUT "COORDINATES OF CORNER RIGHT TOP OF OVERALL PICTURE ";CC,CD
1280  P=3.1415926535
1290  CA=CA/180*P:CB=CB/180*P:A1=C2:A2=-C1:B1=-C1*C3:B2=-C2*C3:B3=C2*C2+C1*C1
1300  CE=SQR(1+C3*C3/B3)/CC*TAN(CA):A1=CE*A1:A2=CE*A2:CE=1/SQR(B3)/CD*TAN(CB)
1310  B1=B1*CE:B2=B2*CE:B3=B3*CE
1320  N1=K1-L1:N2=K2-L2:N3=K3-L3:Z=N1*L1+N2*L2+N3*L3:CA=N1*N1+N2*N2+N3*N3-RR
1330  G=N1*N1-CA:H=N2*N2-CA:I=2*N1*N2:K=2*L1*CA-2*Z*N1:L=2*L2*CA-2*Z*N2
1340  M=Z*Z-CA*(L1*L1+L2*L2+L3*L3)
1350  CA=-A1*Q3:CB=B3*Q1-B1*Q3:CC=C3*Q1-C1*Q3:CD=-A2*Q3:CE=B3*Q2-B2*Q3
1360  CF=C3*Q2-C2*Q3
1380  N=G*CA*CA  +H*CD*CD  +I*CA*CD
1390  O=G*CB*CB  +H*CE*CE  +I*CB*CE        +K*CB*B3
1400  P=G*2*CA*CB+H*2*CD*CE+I*(CA*CE+CB*CD)+K*CA*B3
1410  Q=G*2*CA*CC+H*2*CD*CF+I*(CA*CF+CC*CD)+K*CA*C3
1420  R=G*2*CB*CC+H*2*CE*CF+I*(CB*CF+CC*CE)+K*(CB*C3+CC*B3)
1430  U=G*CC*CC  +H*CF*CF  +I*CC*CF        +K*CC*C3
1450  O=O+L*CE*B3        +M*B3*B3
1460  P=P+L*CD*B3
1470  Q=Q+L*CD*C3
1480  R=R+L*(CE*C3+CF*B3)+M*B3*C3*2
1490  U=U+L*CF*C3        +M*C3*C3
1500  P=-.5*P:Q=-.5*Q
1520  V=CA*N1+CD*N2:W=CB*N1+CE*N2-B3*Z:X=CC*N1+CF*N2-C3*Z
1530  M1=K1-Q1:M2=K2-Q2:M3=K3-Q3:Y=M1*M1+M2*M2+M3*M3-RR
1540  CA=A1*A1+A2*A2:CB=B1*B1+B2*B2+B3*B3:CC=C1*C1+C2*C2+C3*C3:MI=M1*A1+M2*A2
1550  MJ=M1*B1+M2*B2+M3*B3:MK=M1*C1+M2*C2+M3*C3
1560  A=MI*MI-Y*CA
1570  B=MJ*MJ-Y*CB
1580  C=-MI*MJ
1590  D=-MI*MK
1600  E=2*MJ*MK
1610  F=MK*MK-Y*CC
1615  READ SA,TA,SB,TB:SB=SB+SA:TB=TB+TA:GOTO 1640
1620  INPUT "COORDINATES OF LEFT BOTTOM CORNER OF VIEW ";SA,TA
1630  INPUT "SIZE OF VIEW ";SB,TB:SB=SB+SA:TB=TB+TA
1640  INPUT "SKETCH (S) / COMPLETE DRAWING (D)   ";A$:IF A$="D" THEN 800
1650  IF A$<>"S" THEN 1640
1651  :
1652  REM ** SKETCH **
1660  SCREEN $80
1670  LINE 0,0,0,TB-TA,0:LINE SB-SA,0,SB-SA,TB-TA,0:LINE 0,0,SB-SA,0,0
1680  LINE SB-SA,TB-TA,0,TB-TA,0
1690  CC=INT(-C3/B3+.5):IF CC>=TB THEN CC=TB:GOTO 1720
1700  IF CC<=TA THEN CC=TA:GOTO 1750
1710  LINE 0,TB-CC,SB-SA,TB-CC,0
1720  FOR T=TA TO CC:TT=TB-T:GOSUB 410
1721  IF NOT F0 THEN PSET S1-SA,TT,0:PSET S2-SA,TT,0
1730  GOSUB 560:IF NOT F5 THEN PSET S3-SA,TT,0:PSET S4-SA,TT,0
1740  NEXT
1750  FOR T=CC TO TB:TT=TB-T:GOSUB 410
1751  IF NOT F0 THEN PSET S1-SA,TT,0:PSET S2-SA,TT,0
1760  NEXT
1770  GET A$:IF A$<>"C" THEN 1770: REM "C" FOR CONTINUE
1780  SCREEN $02: GOTO 1620
1790  :
2000  REM SPHERE AND LAMP
2010  DATA 0,0,1,1,35,45,6.5
2020  REM PICTURE 1 - PAGE 106
2030  REM DATA -7,-7,1.7
2040  REM DATA 7,6.5,-0.88
2050  REM DATA 11.7,7.5
2051  REM PICTURE 2 - PAGE 104
2052  REM DATA 3.5,-10,3
2053  REM DATA -3.5,8.5,-2.2
2054  REM DATA 11.7,7.5
2055  REM PICTURE 3
2056  REM DATA 22,10,10
2057  REM DATA -29,-18,-10.5
2058  REM DATA 10.47,6.7
2059  REM PICTURE 4
2060  REM DATA -3,-0.7,0.4
2061  REM DATA 3,0,0.4
2062  REM DATA 35.94,23
2063  REM PICTURE 5
2064  DATA 3,3,3
2065  DATA -1,-1,-0.55
2066  DATA 25,16
2070  REM VIEW DATA
2080  DATA 160,100,-160,-100,319,199
